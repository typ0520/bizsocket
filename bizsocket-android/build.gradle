dependencies {
    compile project(':bizsocket-core')
    compile files(getAndroidRuntimeJar().absolutePath)

}

def getAndroidRuntimeJar() {
    def androidHome = getAndroidHome()
    def androidJar = new File("$androidHome/platforms/android-8/android.jar")
    if (androidJar.isFile()) {
        return androidJar
    } else {
        throw new Exception("Can't find android.jar for 8 API. Please install corresponding SDK platform package")
    }
}

def getAndroidHome() {
    def androidHomeEnv = System.getenv("ANDROID_HOME")
    if (androidHomeEnv == null) {
        Properties properties = new Properties()
        properties.load(project.rootProject.file('local.properties').newDataInputStream())
        androidHomeEnv = properties.getProperty('sdk.dir')
    }
    if (androidHomeEnv == null) {
        throw new Exception("ANDROID_HOME environment variable is not set")
    }
    def androidHome = new File(androidHomeEnv)
    if (!androidHome.isDirectory()) throw new Exception("Environment variable ANDROID_HOME is not pointing to a directory")
    return androidHome
}
